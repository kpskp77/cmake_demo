cmake_minimum_required(VERSION 3.0)
project(lib_cat)

# set version number
set(LIB_CAT_VERSION_MAJOR 1)
set(LIB_CAT_VERSION_MINOR 0)
set(LIB_CAT_VERSION_PATCH 0)

# config a header file to pass some of the CMake settings to
# the source code
configure_file(
    ${PROJECT_SOURCE_DIR}/configVersion.hpp.in
    ${PROJECT_BINARY_DIR}/configVersion.hpp
    )

# source file list is seperated from main CMakeLists, includes here
# ${SOURCE_DIR}/source.cmake should populate:
#       1. ${SOURCE_DIR} as source directory
#       2. ${SOURCE_LIST} as source list
include(${PROJECT_SOURCE_DIR}/source.cmake)

# general public headers should be put in ${PROJECT_SOURCE_DIR/include
set(PUBLIC_HEADER_DIRS
    ${PROJECT_SOURCE_DIR}/include
    )
# private headers should be put in ${PROJECT_SOURCE_DIR/src
# ${PROJECT_BINARY_DIR} is for configure_file,
set(PRIVATE_HEADER_DIRS
    ${PROJECT_BINARY_DIR}
    ${SOURCE_DIR}
    )

################### build as shared library ###################
add_library(lib_cat_shared SHARED ${SOURCE_LIST})
# pulic headers are transitive, privates are not
target_include_directories(lib_cat_shared PUBLIC ${PUBLIC_HEADER_DIRS})
target_include_directories(lib_cat_shared PRIVATE ${PRIVATE_HEADER_DIRS})
# generate exported header for dll, compitable cross platform
include(GenerateExportHeader)
generate_export_header(lib_cat_shared
    EXPORT_FILE_NAME ${PROJECT_SOURCE_DIR}/include/lib_cat_EXPORTS.h
    STATIC_DEFINE LIB_CAT_BUILD_AS_STATIC
    )

################### build as static library ###################
add_library(lib_cat_static STATIC ${SOURCE_LIST})
# pulic headers are transitive, privates are not
target_include_directories(lib_cat_static PUBLIC ${PUBLIC_HEADER_DIRS})
target_include_directories(lib_cat_static PRIVATE ${PRIVATE_HEADER_DIRS})
# add compiler definitions to set correct macro for exported headers
# generated by shared libraray above,
# for compitability with static and dynamic mixed build
# set as PUBLIC so that is could be transitive to target depends on this one
target_compile_definitions(lib_cat_static PUBLIC LIB_CAT_BUILD_AS_STATIC)

# install targets
# executable target will be installed in RUNTIME path
# shared library will be installed in RUNTIME path on DLL platform,
#       and will be installed in LIBRARY path on non-DLL platform
# static library will be installed in ARCHIVE path
install(TARGETS lib_cat_shared lib_cat_static
    RUNTIME DESTINATION "bin"
    LIBRARY DESTINATION "lib"
    ARCHIVE DESTINATION "lib"
    )
# install public headers
install(DIRECTORY ${PUBLIC_HEADER_DIRS}
    DESTINATION "."
    )
